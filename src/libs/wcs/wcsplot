#! /bin/csh -f
#
#  wcsplot
#  C shell script
#
#  For each file XXX.fts, run wcs sending output to XXX.out and producing
#  (using gnuplot) a postscript plot XXX.ps of the stars used in the fit
#
#  Last update DJA 021221

set tmp = $0:h/wcsplot.tmp
if (-e $tmp) rm -i $tmp

@ i = 0
@ n = 0
foreach f ($argv)
   @ i++
   if ($f:e == fts) then
      if ($n == 0) @ n = $i
      if (-e $f:r.out) rm -i $f:r.out
      if (-e $f:r.ps)  rm -i $f:r.ps
   endif
end

# $n is index of 1st *.fts in $argv

if ($n == 0) then
   echo "Usage:  $0:t [wcs options] [file1.fts ...]"
endif

@ m = $n - 1
foreach f ($argv[$n-$#argv])
   rm -f /tmp/wcs.gnp
   echo "wcs $argv[1-$m] $f"
   (wcs $argv[1-$m] $f >& $f:r.out) > $f:r.out
   if (-e /tmp/wcs.gnp) then
      set mode = `awk '/size ratio/ {if ($4 > 1) print "portrait"; else print "landscape"}' /tmp/wcs.gnp`
      echo "set terminal postscript $mode color" >! $tmp
      echo "set y2label '$f:r:t'" >> $tmp
      cat /tmp/wcs.gnp >> $tmp
      gnuplot $tmp > $f:r.ps << +

+
   endif
end

rm -f $tmp
